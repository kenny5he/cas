description = "Apereo CAS ‌‌Bill of Material"
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact file("cas-server-support-bom.md")
            pom {
                url = "$projectUrl"
                inceptionYear = project.projectInceptionYear
                name = project.name
                description = project.name
                packaging = "pom"
                issueManagement {
                    system = project.projectIssueSystem
                    url = project.projectIssueUrl
                }
                developers {
                    developer {
                        id = "mmoayyed"
                        name = "Misagh Moayyed"
                    }
                    developer {
                        id = "dima767"
                        name = "Dmitriy Kopylenko"
                    }
                    developer {
                        id = "tsschmidt"
                        name = "Travis Schmidt"
                    }
                    developer {
                        id = "leleuj"
                        name = "Jérôme Leleu"
                    }
                    developer {
                        id = "hdeadman"
                        name = "Hal Deadman"
                    }
                }
            }

            pom.withXml {
                println "Creating BOM with all project dependencies for ${project.name}"

                def dependencyManagement = asNode().appendNode("dependencyManagement")
                def dependencies = dependencyManagement.appendNode("dependencies")

                rootProject.subprojects
                        .findAll { p -> p != project }
                        .each { p ->
                            createDependency(dependencies, groupId, p.name, version)
                        }

                libraries.each {
                    def artifact = it.value
                    if (artifact instanceof List) {
                        artifact.each { art ->
                            if (art.class.isArray()) {
                                art.each { coords ->
                                    def elem = coords.toString().split(":")
                                    createDependency(dependencies, elem[0], elem[1], elem[2])
                                }
                            } else {
                                createDependency(dependencies, art.group, art.name, art.version)
                            }
                        }
                    } else {
                        createDependency(dependencies, artifact.group, artifact.name, artifact.version)
                    }
                }
            }
        }
    }
}

def createDependency(dependencies, group, name, version) {
    def dependency = dependencies.appendNode("dependency")
    dependency.appendNode("groupId", group)
    dependency.appendNode("artifactId", name)
    dependency.appendNode("version", version)
}
